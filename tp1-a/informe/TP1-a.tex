\documentclass[11pt]{article}
\usepackage [spanish,active acute] {babel}
\usepackage{a4wide}
\usepackage{listings}
\lstset{language={[x86masm]Assembler}, basicstyle=\small, numbers=left, numberstyle=\tiny}
%\usepackage[latin1]{inputenc}
\usepackage{graphicx}
\usepackage{algorithm}
\usepackage{algorithmic}[1]

\frenchspacing
\title{
\begin{centering}
Universidad de Buenos Aires \\
Facultad de Ciencias Exactas y Naturales \\
Departamento de Ciencias de la Computaci\'on \\
\vskip 25pt
\bf Organizaci\'on del computador II \\
\bf Segundo cuatrimestre 2009 
\end{centering}
}
\author{
Grupo: \textsc{POPA} \\
\begin{tabular}[t]{|l|l|l|}
\hline
\textbf{Apellido y nombre} & \textbf{L.U.} & \textbf{mail} \\
\hline 
\hline
Cerrutti, Mariano Javier  & 525/07 & vscorza@gmail.com \\
\hline
Huel, Federico Ariel  & 329/07 & federico.huel@gmail.com \\
\hline
Mita, Rogelio Iv\'an  & 635/07 & rogeliomita@gmail.com \\
\hline
\end{tabular}
}
\date{5 de Octubre de 2009} 


\begin{document}
\maketitle
\newpage
\tableofcontents

\newpage
\section{Archivos adjuntos}
\textsc{Implementaci'on} 
\begin{itemize}
\item src:
\subitem \textit{bordes.c}
\subitem \textit{asmSobel.asm}
\subitem \textit{asmPrewitt.asm}
\subitem \textit{asmRoberts.asm}
\item img:
\subitem \textit{lena.bmp}
\end{itemize}

\textsc{Includes}
\begin{itemize}
\item \textit{offset.inc} 
\item \textit{macros.mac} 
\end{itemize}

\textsc{Enunciado} 
\begin{itemize}
\item \textit{EnunciadoTP1A.pdf}  
\end{itemize}

\textsc{Informe} 
\begin{itemize}
\item \textit{TP1-a.pdf}
\end{itemize}

\section{Instrucciones de uso}
Decidimos escribir los c'odigos de las funciones de forma separada para mayor claridad. Utilizamos tambi'en un archivo (\textit{macros.mac}) para definir nuestras macros y  (\textit{offset.inc}) para darle nombres declarativos a los datos de la estructura imagen de \verb-openCV-. En el cd adjunto al trabajo pr'actico, se encuentran todos los archivos fuente clasificados seg'un el tipo. Decidimos utilizar un \verb'makefile' para compilar todos los archivos a la vez. 

\newpage
\section{Introducci'on}
El motivo de este trabajo es ecribir un algoritmo de detecci'on de bordes en lenguaje ensamblador.  Para esto utilizaremos el concepto de derivadas parciales en un espacio de dimensi'on dos dado por el arreglo de p'ixeles que conforman una imagen, de esta manera podemos interesarnos en la derivada parcial respecto de x: $dx$ o la derivada parcial respecto de y: $dy$.\\
En la pr'actica el concepto de derivada parcial se aproxima aplicando operadores especiales (\emph{Roberts},\emph{Prewitt} o \emph{Sobel}) que aplican una matriz de convoluci'on a cada pixel de la imagen.\\
Nuestra motivaci'on a la hora de escribir el c'odigo en lenguaje ensamblador viene dada por la necesidad de conseguir una soluci'on eficiente en t'erminos de tiempo para resolver el problema de detectar bordes en una imagen de tama\~{n}o arbitrario. \\
\begin{center}
\begin{minipage}{11cm}
 Las funciones en c'odigo ensamblador estan divididas seg'un el operador que se desea aplicar a la imagen para buscar bordes.
\end{minipage}
\end{center}


\section{Desarrollo}
Antes de presentar los algoritmos pertinentes a cada funci'on hacemos algunas anotaciones generales:
\begin{itemize}
\item Nos fue pedido que en todos los algoritmos sea respetada la convenci'on \verb'C', de forma que la construcci'on del \verb'stack frame' (guardado de los registros edi, esi y ebx, ajuste de la pila, etc) se encuentra al principio de cada funci'on dentro del macro \verb'doEnter' junto con su contrapartida que se encuentra definida en el macro \verb'doLeave'.
\item En todas las funciones, consideramos que los par'ametros pasados por referencia (excepto aquellos que deb'ian cambiarse expl'icitamente de acuerdo al enunciado) no deb'ian ser modificados, y por lo tanto, los mismos han sido guardados en variables locales o bien en registros. En algunos casos esto es muy importante, por ejemplo, cuando nos pasan un puntero a una lista, si se modifica ese puntero, se pierde la direcci'on de esa lista luego de llamar a la funci'on; y esto no puede ocurrir.
\item Tambi'en tuvimos en cuenta que en las im'agenes las filas de p'ixeles se guardan en memoria de forma alineada.
\item Por cuestiones de simplicidad decidimos que los algoritmos tambi'en procesar'an la basura (restos de bytes en memoria) que era inclu'ida al alinear la imagen, ya que al momento de la visualizaci'on ser'an considerados aquellos p'ixeles contenidos dentro del ancho y el alto definido para la imagen.
\end{itemize}
A continuaci'on se exponen las funciones implementadas, junto con sus respectivos c'odigos en lenguaje ensamblador y se explica el uso que se da a los registros de prop'osito general y el funcionamiento de las mismas en l'ineas generales.
\subsection{Enfoque general}
Para el c'alculo aproximado de las derivadas parciales aplicamos matrices de convoluci'on, que nos permiten aplicar un valor a un pixel de destino a partir de operar suma de productos a sus p'ixeles circundantes en la imagen de origen, en el ejemplo de la derivada parcial x para el operador de \verb'Sobel' la matriz tendr'ia este aspecto:
\begin{center}
\begin{minipage}{5in}
\[ \left( \begin{array}{ccc}
-1 & 0 & 1 \\
-2 & 0 & 2 \\
-1 & 0 & 1 \end{array} \right)\]
\end{minipage}
\end{center}

Podemos explicar su aplicaci'on con el siguiente pseudoc'odigo:

 \begin{algorithmic}[1]
\STATE $(\forall p_{i,j} : i \in ancho(imagenOrigen), j \in alto(imagenOrigen))(p_{i,j} \in [0-255])$
\FOR{$i \in ancho(imagenOrigen)$}
\FOR{$j \in alto(imagenOrigen)$}
\STATE $p_{i,j} \leftarrow -p_{i-1,j-1} - 2 * p_{i-1,j} - p_{i-1,j+1} +p_{i+1,j-1} + 2 * p_{i+1,j} + p_{i+1,j+1}$\label{lin:linea_asignapixel}
\ENDFOR
\ENDFOR
 \end{algorithmic}

Esta es la manera en la que aplicaremos matrices de convoluci'on a nuestras im'agenes para conseguir nuevas im'agenes que contienen la informaci'on de las derivadas parciales para intentar detectar bordes, i.e. cambios bruscos en la intensidad de la imagen respecto de la direcci'on de abscisas y ordenadas o en ambas.

\subsection{Archivos inclu'idos}
\subsubsection{offset.inc}
Aqu'i se encuentran los desplazamientos a dato para cada una de las estructuras que usamos para la imagen del \verb-openCV- por motivos de claridad en el c'odigo
\begin{lstlisting}[frame=single]
; typedef struct _IplImage
; {
;     int nSize;
;     int ID;
;     int nChannels;
;     int alphaChannel;
;     int depth;
;     char colorModel[4];
;     char channelSeq[4];
;     int dataOrder;
;     int origin;
;     int align;
;     int width;
;     int height;
;     struct _IplROI *roi;
;     struct _IplImage *maskROI;
;     void *imageId;
;     struct _IplTileInfo *tileInfo;
;     int imageSize;
;     char *imageData;
;     int widthStep;
;     int BorderMode[4];
;     int BorderConst[4];
;     char *imageDataOrigin;
; }
; IplImage;

%define DEPTH		16
%define WIDTH		40
%define HEIGHT		44
%define IMAGE_DATA 	68
%define WIDTH_STEP	72
\end{lstlisting}

\subsubsection{macros.mac}
Estos son las macros utilizadas en cada funci'on.  Para este trabajo s'olo utilizamos \verb'doEnter' y \verb'doLeave' pero se han mantenido las macros restantes por suponer su utilidad futura.
\begin{lstlisting}[frame=single]
;========================
; MACRO doEnter
;========================
; Escribe el encabezado que arma el stack frame
; Entrada:
;	tamanio		tamanio de memoria a reservar al entrar al proc
;========================
%macro doEnter 0-1 0
	push ebp
	mov ebp, esp
%if %1 <> 0
	sub esp, %1
%endif
	push edi
	push esi
	push ebx
%endmacro
;========================
; MACRO doLeave
;========================
; Escribe la salida que restaura el stack frame previo
; Entrada:
;	tamanio		tamanio de memoria reservada al entrar al proc
;	doRet		se marca en 1 si debe llamar a ret
;========================
%macro doLeave 0-2 0,0
	pop ebx
	pop esi
	pop edi
%if %1 <> 0
	add esp, %1
%endif
	pop ebp
%if %2 <> 0
	ret
%endif	
%endmacro

;========================
; MACRO doWrite
;========================
; Escribe una cadena a consola
; Entrada: 
;	mensaje		direccion de comienzo de la cadena
;	len		largo de la cadena a escribir
;========================
%macro doWrite 1
	%%msg: db %1
	%%len: equ $- %%msg	
	mov eax,4			;inicializa escritura a consola
	mov ebx,1
	mov ecx,%%msg
	mov edx,%%len
	int 80h
%endmacro
;========================
; MACRO doEnd
;========================
; Termina la ejecucion con el codigo deseado
; Entrada: 
;	codigo		codigo de error deseado, cero en su defecto
;========================
%macro doEnd 0-1 0
	mov eax,1
	mov ebx,%1
	int 80h
%endmacro
;========================
; MACRO doMalloc
;========================
; Pide la cantidad especificada de memoria
; Entrada:
;	cantidad	cantidad de memoria a reserver
;========================
%macro doMalloc 1
	push %1
	call malloc
	add esp, 4
%endmacro
;========================
; MACRO doRetc
;========================
; Retorna si se cumple la condicion especificada
; Entrada:
;	condicion	condicion ante la cual retornar
;========================
%macro  doRetc 1 
        j%-1    %%skip 
        ret 
  %%skip: 
%endmacro
;========================
; MACRO doWriteFile
;========================
; Escribe a archivo solo si esta definido el DEBUG
; Entrada:
;	arch		puntero al archivo
;	msg		texto a escribir
; Uso:
;	doWriteFile [FHND], {"hola mundo",13,10}
;========================
%macro  doWriteFile 2+ 
%ifdef DEBUG
        jmp     %%endstr 
  %%str:        db      %2 
  %%endstr: 
        mov     dx,%%str 
        mov     cx,%%endstr-%%str 
        mov     bx,%1 
        mov     ah,0x40 
        int     0x21 
%endif
%endmacro

\end{lstlisting}

\newpage
\subsection{Recorrido de la matriz de imagen}
\begin{enumerate}
\item \textbf{Cosas que tuvimos en cuenta} 
\subitem La matriz fue recorrida procesando la basura, i.e, en todo el ancho de la imagen alineada \\
\subitem Cada pixel ocupa exactamente un byte, ya que la imagen que recibe la funci'on es en escala de grises (0-255)  \\
\subitem En la arquitectura \verb'IA-32' los datos son guardados en memoria de forma \emph{little-endian}, por lo que el byte m'as significativo de un dato se encuentra en la posici'on de memoria m'as alta.
\end{enumerate}

\subsection{Funciones implementadas}
\begin{itemize}
\item Intentamos reducir el acceso a memoria utilizando registros para aquellas operaciones que se encontraban dentro de los ciclos, evitando as'i m'ultiples accesos a memoria. Si bien esto llev'o a tener un codigo menos legible, lo explicaremos en detalle.
\item Variables locales
\subitem \textit{SRC} Contiene el puntero a la imagen de entrada.
\subitem \textit{DST} Contiene el puntero a la imagen resultado.
\subitem \textit{WIDTH} Contiene el tamano de ancho de la imagen.
\subitem \textit{HEIGHT} Contiene la altura de la imagen.
\subitem \textit{XORDER} Variable que especifica si el c'alculo debe hacerse sobre la derivada X.
\subitem \textit{YORDER} Variable que especifica si el c'alculo debe hacerse sobre la derivada Y.
\subitem \textit{T\_HEIGHT} Variable que se usa como contador para el recorrido vertical.
\item Registros de prop'osito general 
\subitem \textit{eax} Se utiliza como acumulador, para realizar los c'alculos de los p'ixeles.
\subitem \textit{ebx} Se utiliza como acumulador, para realizar los c'alculos de los p'ixeles.
\subitem \textit{ecx} Se utiliza para recorrer la imagen de entrada.
\subitem \textit{edx} Se utiliza como contador para el recorrido horizontal de la imagen.
\subitem \textit{esi} Se utiliza para recorrer la imagen de salida.
\subitem \textit{edi} Contiene el ancho de la imagen.
\item \textbf{Funcionamiento}
\subitem Lo primero que hacen todos los algoritmos es ubicar al registro \verb'ecx' en el comienzo de la imagen de entrada y a \verb'esi' en el primer pixel que se modificar'a en la imagen de salida.\\
Luego comienza el ciclo que recorre la matriz por filas, para lo que carga en \verb'edx' el ancho de la imagen, us'andolo como contador del recorrido horizontal, restando los pixeles laterales ya que no se procesar'an debido a la dimensi'on de la matriz.
\end{itemize}

\subsubsection{Sobel}
\begin{enumerate}
Presentaremos la el c'odigo de la funci'on completa de sobel para ver el funcionamiento en totalidad y en el resto de los operadores se har'a mencion solo al fragmento de codigo que realiza las operaciones importantes:
\item \textbf{X Sobel}
\subitem Dentro del ciclo que recorre la imagen horizontalmente, se copian en \verb'eax' los cuatro pixeles que se encuentran a continuaci'on en la imagen y mediante una m'ascara se obtienen s'olo el primero y el tercero. Luego se hace lo mismo para los pixeles que se encuentran en la siguiente l'inea guard'andolos en \verb'ebx', y desplaz'andolos un bit a izquierda para duplicar su valor. Se suman ambos registros y y se guarda el resultado en \verb'eax'.\\
Se cargan los pixeles de la tercer l'inea en \verb'ebx', se le aplica una m'ascara para obtener s'olo los necesarios y se los suma a los acumulados quedando en la parte alta de \verb'eax' la suma de los p'ixeles de la primera fila y en la parte alta la de los p'ixeles de la tercera fila. Luego, se copia  \verb'eax' a \verb'ebx', y se desplaza a \verb'eax' para que queden los p'ixeles que deben restarse en la parte menos significativa. 
Con los datos listos, se realiza la resta ($ax \leftarrow ax - bx$). Aqui se presenta el fragmento de codigo que realiza esto:
\item \textbf{Y Sobel}
\subitem Dentro del ciclo que recorre la imagen horizontalmente, se cargan en \verb'eax' y en \verb'ebx' cuatro p'ixeles en los que los primeros tres contienen los p'ixeles en el orden que ser'an sumados de acuerdo al c'alculo de \verb'Sobel'.\\
Luego se aplica una m'ascara a \verb'eax' que deja s'olo el primer y tercer pixel y otra en \verb'ebx' que deja solo el segundo. Se hace un desplazamiento a derecha a \verb'ebx' para ubicar el pixel en la parte baja del registro y luego realizar la suma con la parte baja de \verb'eax' i.e. el segundo y el tercer pixel, dejando un bit a derecha del segundo pixel para duplicar su valor (seg'un indican los coeficientes del operador). A continuaci'on se suman el primer y segundo pixel (duplicado en valor), se desplaza el tercero y se lo suma a los dos anteriores, quedando en el registro \verb'ebx' el valor de la suma de los p'ixeles de la primer fila.\\
Luego se copian en \verb'eax' los cuatro p'ixeles que contienen a los que deben restarse y se elimina el que no se necesita mediante la utilizaci'on de una m'ascara como se hiciera para la primera fila. Para poder operar, se utiliza la funci'on \verb'ror' que deja en la palabra menos significativa s'olo un pixel y poder restarlo llamando a \verb'ax'. Luego se borra la parte baja de \verb'eax' y se guarda la parte alta en \verb'ebx' (La parte alta de \verb'ebx' no estaba siendo utilizada ya que acumula solo la suma de tres Words). Se desplaza \verb'eax' para que quede el tercer pixel en la parte baja y se lo resta al acumulador. Lo mismo se hace con el segundo pixel, pero dejando un bit a derecha al desplazarlo para que su valor sea el doble.
\item \textbf{En todos los caso}
\subitem Antes de copiarlo, se comprueba que el pixel calculado no se haya saturado, y en caso de que eso haya sucedido lo que se copia es el mayor o menor valor para el pixel dentro del rango de \verb'byte', de acuerdo a la saturacion, evitando desbordes.\\
Luego se restan los contadores corresopondientes y se comprueba si ha finalizado alguno de los ciclos para decidir que salto se realiza.
\begin{lstlisting}[frame=single]
;void asmSobel(const char* src, char* dst, int ancho, int alto, int xorder, int yorder)

%include "include/defines.inc"
%include "include/offset.inc"
%include "include/macros.mac"

%define SRC	[ebp + 8]
%define DST	[ebp + 12]
%define WIDTH 	[ebp + 16]
%define HEIGHT	[ebp + 20]
%define XORDER	[ebp + 24]
%define YORDER	[ebp + 28]
%define T_HEIGHT	[ebp - 4]

;=================
; doCorridaX
;=================
; Pinta una linea en horizontal de negro
; =================
%macro doCorridaX 0
	mov ebx,	edi
	%%corridaX:
		and byte [esi], bl
		inc esi		
		dec ebx
		jnz %%corridaX
%endmacro

global asmSobel

section .data

section .text

asmSobel:
	doEnter 1

	mov eax, HEIGHT
	mov dword T_HEIGHT, eax

	mov	edx,	SRC
	mov	edi,	[edx + WIDTH_STEP]
	
	mov	ecx,	SRC	;ecx registro para el pixel de origen
	mov	ecx,	[ecx + IMAGE_DATA]

	mov	esi,	DST	;esi registro para el pixel de destino
	mov	esi,	[esi + IMAGE_DATA]

	;=============================
	; limpiamos la imagen
	;=============================

	cicloYcls:
	 mov edx, edi
	  cicloXcls:
	      mov	dword [esi],	0	;mando el pixel
	      inc	ecx
	      inc	esi
	      dec edx
	      jnz cicloXcls
	  dec	dword T_HEIGHT
	  jnz	cicloYcls
	
	cmp dword XORDER, 0
	je	ySobel

	xSobel:
	;==========================
	; SOBEL XORDER
	;==========================
	mov eax, HEIGHT
	mov dword T_HEIGHT, eax

	mov	ecx,	SRC		;ecx registro para el pixel de origen
	mov	ecx,	[ecx + IMAGE_DATA]

	mov	esi,	DST		;esi registro para el pixel de destino
	mov	esi,	[esi + IMAGE_DATA]

	add	esi,	edi		;salteo la primera linea
	inc	esi			;salteo la primer columna

	cicloY:
	 mov edx, edi
	  sub	edx,	2		;le resto los pixeles laterales
	  
	  cicloX:
	      mov	eax,	[ecx]	;cargo cuatro pixeles en eax
	      and	eax,	0x00FF00FF	;paso a dos words empaquetadas
	      mov	ebx,	[ecx + edi]	;sumo dos veces la segunda linea
	      and	ebx,	0x00FF00FF	;paso a dos words empaquetadas
	      shl	ebx,1	;desplazo siete bits a derecha para permitir 
				;operaciones en 8 bits
	      add	eax,	ebx
	      mov	ebx,	[ecx + edi * 2]	;sumo la tercera linea
	      and	ebx,	0x00FF00FF	;paso a dos words empaquetadas
	      add	eax,	ebx
	      mov	ebx,	eax
	      shr	eax,	16	;muevo eax a la parte izquierda de la matriz
	      sub	ax,	bx	;se la resto al pixel destino
	      cmp	ax,	0x00FF
	      jle	noSobreSatura	;estos jumps son para evitar el error
					;de salto fuera de rango
	      jmp	sobresaturo
	noSobreSatura:
	      cmp	ax,	0x0000
	      jge	volver
	      jmp	subsaturo
	      volver:

	      add	[esi],	al	;mando el pixel
	      inc	ecx
	      inc	esi
	      dec edx
	      jnz cicloX
	  add	ecx,	2	;sumo dos para llevar al primero de la linea siguiente
	  add	esi,	2

	  dec	dword T_HEIGHT
	  jnz	cicloY

	ySobel:

	cmp dword YORDER, 0
	jne	sigueYSobel
	jmp	pintaBordes
	sigueYSobel:
	;==========================
	; SOBEL YORDER
	;==========================
	mov eax, HEIGHT
	mov dword T_HEIGHT, eax

	mov	edx,	SRC
	mov	edi,	[edx + WIDTH_STEP]
	
	mov	ecx,	SRC		;ecx registro para el pixel de origen
	mov	ecx,	[ecx + IMAGE_DATA]

	mov	esi,	DST		;esi registro para el pixel de destino
	mov	esi,	[esi + IMAGE_DATA]

	add	esi,	edi		;salteo la primera linea
	inc	esi			;salteo la primer columna

	cicloY2:
	 mov edx, edi
	  sub	edx,	2		;le resto los pixeles laterales

	  cicloX2:
	      mov	eax,	[ecx + edi * 2]		;cargo cuatro pixeles en eax
	      mov	ebx,	eax		;copio a ebx
	      and	eax,	0x00FF00FF	;paso a dos words empaquetadas
	      and	ebx,	0x0000FF00	;hago lo mismo con el pixel del medio
	      shr	ebx,	7
	      add	bx,	ax		;acumulo el primero con el segundo
	      and	ebx,	0x0000FFFF
	      shr	eax,	16
	      add	bx,	ax		;ya acumulamos los primeros en bx

	      mov	eax,	[ecx]	;cargo la tercera linea
	      and	eax,	0x00FFFFFF
	      ror	eax,	16		;paso la parte izq a der i.e. me queda el primer
						; pixel en ax en una word
	      sub	bx,	ax		;resto el primer pixel
	      xor	ax,	ax		;borro la parte baja me quedan los otros dos arriba
	      or	ebx,	eax		;paso la parte alta de eax a ebx 
						;como contenedor temporal queda: px2 px3 \ acum
	      shr	eax,	16		;bajo los dos pixeles altos
	      and	eax,	0x000000FF	;me quedo con el tercer pixel
	      sub	bx,	ax		;resto el tercer pixel
	      mov	eax,	ebx		;voy a recuperar el segundo pixel
	      and	eax,	0xFF000000
	      shr	eax,	23		;lo paso a derecha multiplicado por dos
	      sub	bx,	ax		;resto el segundo pixel
	      cmp	bx,	0x00FF
	      jg	sobresaturo2
	      cmp	bx,	0x0000
	      jl	subsaturo2
	      volver2:

	      or	[esi],	bl	;mando el pixel
	      noPintar2:
	      inc	ecx
	      inc	esi
	      dec edx
	      jnz cicloX2
	  add	ecx,	2	;sumo dos para llevar al primero de la linea siguiente
	  add	esi,	2
	  dec	dword T_HEIGHT
	  jnz	cicloY2


	pintaBordes:
	mov esi, DST		;esi registro para el pixel de destino
	mov esi, [esi + IMAGE_DATA]

	; pinta las columnas primera y ultima

	mov	edx,	SRC
	mov	edi,	[edx + WIDTH_STEP]

	mov ecx, HEIGHT
	mov ebx, WIDTH
	
	ciclo:
		cmp ecx, 0
		je abort
		mov byte [esi], 0x0
		mov eax, esi
		lea esi, [esi + ebx]
		mov byte [esi-1], 0
		mov esi, eax
		add esi, edi
		dec ecx
		jmp ciclo

	abort:
	doLeave 1, 1

	      sobresaturo:
	      mov	eax,	0x000000FF
	      jmp	volver
	      subsaturo:
	      mov	eax,	0
	      jmp	volver

	      sobresaturo2:
	      mov	ebx,	0x000000FF
	      jmp	volver2
	      subsaturo2:
	      mov	ebx,	0
	      jmp	volver2
\end{lstlisting}

\subsubsection{Prewitt} 
\begin{enumerate}
\item \textbf{X Prewitt}
\subitem Dentro del ciclo horizontal, se copian en eax los cuatro pixeles que se encuentran a continuaci'on en la imagen y mediante una m'ascara se obtienen s'olo el primero y el tercero. Luego se hace lo mismo para los pixeles que se encuentran en la siguiente linea guard'andolos en \verb'ebx'. Se suman ambos y se gurada el resultado en \verb'eax'.
Se cargan los pixeles de la tercer l'inea en \verb'ebx', se le aplica una m'ascara para obtener solo los necesarios y se los suma a los acumulados quedando en la parte alta de \verb'eax' la suma de los primeros pixeles de cada fila y en la parte baja los terceros. Luego, se copia  \verb'eax' a \verb'ebx', y se lo desplaza \verb'eax' 16 bits a derecha para que queden los pixeles que deben restarse en la parte menos significativa.
Con los datos listos, se realiza la resta. Aqui se presenta el fragmento de codigo que realiza esto:
\begin{lstlisting}[frame=single]
	      mov	eax,	[ecx]	;cargo cuatro pixeles en eax
	      and	eax,	0x00FF00FF	;paso a dos words empaquetadas
	      mov	ebx,	[ecx + edi]	;sumo dos veces la segunda linea
	      and	ebx,	0x00FF00FF	;paso a dos words empaquetadas
	      add	eax,	ebx
	      mov	ebx,	[ecx + edi * 2]	;sumo la tercera linea
	      and	ebx,	0x00FF00FF	;paso a dos words empaquetadas
	      add	eax,	ebx
	      mov	ebx,	eax
	      shr	eax,	16	;muevo eax a la parte izquierda de la matriz
	      sub	ax,	bx	;se la resto al pixel destino
	      cmp	ax,	0x00FF
	      jle	noSobreSaturo
	      jmp	sobresaturo6
	      noSobreSaturo:
	      cmp	ax,	0x0000
	      jge	volver6
	      jmp	subsaturo6
	      volver6:
	--------------------------------------------------------------
	...
	--------------------------------------------------------------
	sobresaturo6:
	      mov	al,	0xFF
	      jmp	volver6
	subsaturo6:
	      mov	al,	0
	      jmp	volver6

\end{lstlisting}
\item \textbf{Y Prewitt}
\subitem Dentro del ciclo que recorre la imagen horizontalmente, se cargan en \verb'eax' y en \verb'ebx' cuatro pixeles en los que los primeros tres contienen en orden los p'ixeles que ser'an sumados de acuerdo al c'alculo de Prewitt.
Luego se aplica una m'ascara a \verb'eax' que deja s'olo el primer y tercer pixel, y otra en \verb'ebx' que deja s'olo el segundo. Se hace un desplazamiento a derecha a \verb'ebx' para ubicar el pixel en la parte baja del registro para realizar la suma con los otros pixeles. A continuaci'on se suman el primer y segundo pixel, se deslplaza el tercero y se lo suma a los dos anteriores, quedando en el registro \verb'ebx' el valor de la suma de los pixeles.
Luego se copian en \verb'eax' los cuatro pixeles que contienen a los que deben restarse y se elimina el que no se necesita mediante la utilizaci'on de una m'ascara. Para poder operar, se utiliza la funci'on \verb'ror' para dejar en la palabra menos significativa solo un pixel y poder restarlo llamando a \verb'ax'. Luego se borra la parte baja de \verb'eax' y se guarda la parte alta en \verb'ebx' (La parte alta de \verb'ebx' no estaba siendo utilizada ya que acumula s'olo la suma de tres Words). Se desplaza \verb'eax' para que quede el tercer pixel en la parte baja y se lo resta al acumulador. Lo mismo se hace con el segundo pixel. Aqui se presenta el fragmento de codigo que realiza esto:
\begin{lstlisting}[frame=single]
	mov	eax,	[ecx + edi * 2]	;cargo cuatro pixeles en eax
	mov	ebx,	eax		;copio a ebx
	and	eax,	0x00FF00FF	;paso a dos words empaquetadas
	and	ebx,	0x0000FF00	;hago lo mismo con el pixel del medio
	shr	ebx,	8
	add	bx,	ax		;acumulo el primero con el segundo
	and	ebx,	0x0000FFFF
	shr	eax,	16
	add	bx,	ax		;ya acumulamos los primeros en bx
	
	mov	eax,	[ecx]	;cargo la tercera linea
	and	eax,	0x00FFFFFF
	ror	eax,	16		;paso la parte izq a der i.e. me 
					;queda el primer pixel en ax en una word
	sub	bx,	ax		;resto el primer pixel
	xor	ax,	ax		;borro la parte baja me quedan los otros dos arriba
	or	ebx,	eax		;paso la parte alta de eax a ebx como contenedor 
					;temporal queda: px2 px3 \ acum
	shr	eax,	16		;bajo los dos pixeles altos
	and	eax,	0x000000FF	;me quedo con el tercer pixel
	sub	bx,	ax		;resto el tercer pixel
	mov	eax,	ebx		;voy a recuperar el segundo pixel
	and	eax,	0xFF000000
	shr	eax,	24		;lo paso a derecha multiplicado por dos
	sub	bx,	ax		;resto el segundo pixel
	cmp	bx,	0x00FF
	jg	sobresaturo7
	cmp	bx,	0x0000
	jl	subsaturo7
	--------------------------------------------------------------
	...
	--------------------------------------------------------------
	sobresaturo7:
	      mov	bl,	0xFF
	      jmp	volver7
	subsaturo7:
	      mov	bl,	0
	      jmp	volver7
\end{lstlisting}
\item \textbf{Todos los casos}
\subitem Antes de copiarlo, se comprueba que el pixel calculado no se haya saturado, y en caso de que eso haya sucedido lo que se copia es el mayor o menor valor para el pixel, de acuerdo a la saturacion, evitando desbordes.
Luego se restan los contadores corresopondientes y se comprueba si ha finalizado alguno de los ciclos para decidir que salto se raliza.
\item \textbf{C'odigo}
\end{enumerate}

\subsubsection{Roberts} 
\begin{enumerate}
\item \textbf{X Roberts}
\subitem Comienza el ciclo horizontal. Se cargan los tres pixeles a los que est'a apuntando \verb'ecx' (que es el que recorre la imagen) y se utiliza una m'ascara para quedarse s'olo con el primer pixel en \verb'ax', ya que es el que se utilizar'a en el c'alculo.
Se copian en \verb'bx' los dos pixeles que se encuentran en la siguiente linea, se lo enmascara para quedarse con el m'as significativo y se lo desplaza para poder operar con el pixel guardado en \verb'ax'.
Luego se realiza la resta entre \verb'bx' y \verb'ax', se verifica si hay saturaci'on y se copia el pixel guardado en al en la imagen de salida. Aqui se presenta el fragmento de codigo que realiza esto:
\begin{lstlisting}[frame=single]
	mov	ax,	[ecx]		;cargo 4 pixeles en eax
	and	ax,	0x00FF
	
	mov	bx,	[ecx + edi]	;sumo dos veces la segunda linea
	shr	bx,	8	;desplazo 8 bits a derecha para permitir
				;operaciones en 8 bits
	
	sub	ax,	bx		;se la resto al pixel destino
	cmp	ax,	0x00FF
	jle	noSobreSaturo
	jmp	sobresaturo
	noSobreSaturo:
	cmp	ax,	0x0000
	jge	volver
	jmp	subsaturo
	--------------------------------------------------------------
	...
	--------------------------------------------------------------
	sobresaturo:
		mov	eax,	0x000000FF
		jmp	volver
	subsaturo:
		mov	eax,	0
		jmp	volver
\end{lstlisting}
\item \textbf{Y Roberts}
\subitem En el ciclo horizontal, se copian en \verb'bx' los pixeles necesarios qued'andose solo con el segundo, que es el que se utilizar'a. Se lo desplaza 8 bits a derecha para que quede en la parte menos significativa y que se pueda operar con otro pixel. Luego se copia el pixel que est'a en la linea siguiente en \verb'ax' usando una m'ascara para filtrar el byte m'as significativo y se realiza la resta entre \verb'bx' y \verb'ax'. Se verifica la aparici'on de saturaci'on y se copia el pixel en lugar correspondiente de la imagen de salida.
Se incrementa los registros que recorren las im'agenes y se aumenta de l'inea si no deben escribirse m'as pixeles en dicha l'inea y vuelve a comenzar el ciclo. Aqui se presenta el fragmento de codigo que realiza esto:
\begin{lstlisting}[frame=single]
	mov	bx,	[ecx]		;cargo la primera linea
		shr	bx, 8		;lo llevo a la parte menos significativa
		
	mov	ax,	[ecx + edi]	;cargo un pixel en eax
	and	ax,	0x00FF		;paso a dos words empaquetadas
		
	sub	bx,	ax		;resto el  pixel
	cmp	bx,	0x00FF
	jg	sobresaturo2
	cmp	bx,	0x0000
	jl	subsaturo2
	--------------------------------------------------------------
	...
	--------------------------------------------------------------
	sobresaturo2:
	      mov	ebx,	0x000000FF
	      jmp	volver2
	subsaturo2:
	      mov	ebx,	0
	      jmp	volver2
\end{lstlisting}
\item \textbf{En todo caso}
\subitem Antes de copiarlo, se comprueba que el pixel calculado no se haya saturado, y en caso de que eso haya sucedido lo que se copia es el mayor o menor valor para el pixel, de acuerdo a la saturacion, evitando desbordes.
Luego se restan los contadores corresopondientes y se comprueba si ha finalizado alguno de los ciclos para decidir que salto se realiza.
\item \textbf{C'odigo}
\end{enumerate}


\newpage
\section{Resultados}
Estos resultados se cuentan en ticks y reflejan un promedio de los tiempos transcurridos durante la ejecuci'on de cada algoritmo en 100 corridas.
\begin{center}
 \begin{tabular}{| r | c |}
\hline
Algoritmo	&	Tiempo en ticks \\
\hline
Sobel en x (\verb'openCV')	&	9232651\\
Sobel en y (\verb'openCV')	&	8605809\\
Sobel en x e y (\verb'openCV')	&	8715342\\
\hline
Sobel en x (\verb'asm')	&	8759303\\
Sobel en y (\verb'asm')	&	8465688\\
Sobel en x e y (\verb'asm')	&	16477878\\
\hline
\end{tabular}
\end{center}
\section{Conclusiones}
Luego de implementar este trabajo en lenguaje ensamblador, podemos concluir que si bien nos da una gran libertad a la hora de programar (modificar directamente la memoria, trabajar con los registros directamente, etc.), la claridad del c'odigo de estos programas no es tal como los de lenguaje de alto nivel que manejamos \textit{C}, \textit{C++}, etc. \\
De todas formas, consideramos que la realizaci'on de este trabajo nos sirvi'o para obtener un importante conocimiento sobre el lenguaje de bajo nivel, el cual complementa entender cosas de lenguajes de m'as alto nivel, y consideramos adem'as que es una muy buena pr'actica para ayudarnos a comprender el funcionamiento interno de un computador.

\end{document}
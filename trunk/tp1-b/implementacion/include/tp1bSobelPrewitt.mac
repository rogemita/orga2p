%define REMAINDER	[ebp - 4]
%define	STEP_X		14
%define	STEP_Y		14
%define	FINAL_STEP	2
%define RESERVED_BYTES	4

;========================
; MACRO obtenerBajo
;========================
; Recupera la parte alta de un dato empaquetado a byte
; Entrada:
;	registro1	registros sobre los cuales operar 
;	registro2
;	registro3
;	offset		indica si debe desplazar el dato original
;========================
%macro obtenerBajo 2-3 0
	movdqu		%1, %2		;copio el dato
%if %3 != 0
	psrldq		%1, %3		;desplazo dos columnas
%endif
	punpcklbw	%1, %1		;desempaqueto la parte baja
	psllw		%1, 8		;retiro el excedente alto
	psrlw		%1, 8
%endmacro

%macro obtenerAlto 2-3 0
	movdqu		%1, %2		;copio el dato
%if %3 != 0
	psrldq		%1, %3		;desplazo dos columnas
%endif
	punpckhbw	%1, %1		;desempaqueto la parte baja
	psllw		%1, 8		;retiro el excedente alto
	psrlw		%1, 8
%endmacro      

;========================
; MACRO sobelPrewittX
;========================
; Cuerpo de codigo que aplica los operadores de Sobel y Prewitt en el orden x
; Entrada:
;	registro1	registros sobre los cuales operar 
;	registro2
;	registro3
;	duplica?	debe duplicar el valor del primer registro?
;	procesaAmbos	indica si debe procesar el dato bajo y el alto
;========================
%macro	sobelPrewittX 4-5 0
	;**************************
	; VOY A APLICAR EL OPERADOR
	; EN LA PARTE BAJA
	;**************************
	obtenerBajo	xmm6, %2, 2	;obtengo los bajos de la segunda fila
%if %4 = 0
	psllw		xmm6, 1		;multiplico por dos
%endif
	obtenerBajo	xmm7, %1, 2	;obtengo los bajos de la primera fila
	paddusw		xmm6, xmm7	;sumo saturado
	obtenerBajo	xmm7, %3, 2	;obtengo los bajos de la tercera fila
	paddusw		xmm6, xmm7	;sumo saturado

	obtenerBajo	xmm7, %2		;obtengo los bajos de la segunda fila
%if %4 = 0
	psllw		xmm7, 1		;multiplico por dos
%endif
	psubusw		xmm6, xmm7
	obtenerBajo	xmm7, %1		;obtengo los bajos de la primera fila
	psubusw		xmm6, xmm7	;sumo saturado
	obtenerBajo	xmm7, %3		;obtengo los bajos de la tercera fila
	psubusw		xmm6, xmm7	;sumo saturado

	;************************
	; TERMINE COPIO A DESTINO
	;************************
	packuswb	xmm6, xmm6

	movq		[eax], xmm6	;copio los 8 bytes al destino
	

%if %5 = 0
	add		eax, 8	;salto la linea siguiente
	;**************************
	; VOY A APLICAR EL OPERADOR
	; EN LA PARTE ALTA
	;**************************	
	obtenerAlto	xmm6, %2, 2	;obtengo los bajos de la segunda fila
%if %4 = 0
	psllw		xmm6, 1		;multiplico por dos
%endif
	obtenerAlto	xmm7, %1, 2	;obtengo los bajos de la primera fila
	paddusw		xmm6, xmm7	;sumo saturado
	obtenerAlto	xmm7, %3, 2	;obtengo los bajos de la tercera fila
	paddusw		xmm6, xmm7	;sumo saturado

	obtenerAlto	xmm7, %2		;obtengo los bajos de la segunda fila
%if %4 = 0
	psllw		xmm7, 1		;multiplico por dos
%endif
	psubusw		xmm6, xmm7
	obtenerAlto	xmm7, %1		;obtengo los bajos de la primera fila
	psubusw		xmm6, xmm7	;sumo saturado
	obtenerAlto	xmm7, %3		;obtengo los bajos de la tercera fila
	psubusw		xmm6, xmm7	;sumo saturado

	;************************
	; TERMINE COPIO A DESTINO
	;************************
	packuswb	xmm6, xmm6
	movq		[eax], xmm6	;copio los 4 bytes al destino de los cuales 2 son validos
	sub		eax, 8
%endif
	add		eax, edx	;salto la linea siguiente
%endmacro

;========================
; MACRO sobelPrewittY
;========================
; Cuerpo de codigo que aplica los operadores de Sobel y Prewitt en el orden y
; Entrada:
;	registro1	registros sobre los cuales operar 
;	registro2
;	duplica?	debe duplicar el valor del primer registro?
;	procesaAmbos	indica si debe procesar el dato bajo y el alto
;========================
%macro	sobelPrewittY 2-3 0
	;**********************
	; PROCESO PIXEL 2,4,6,8
	;**********************
	movdqu	xmm5, %1			;cargo el primer registro y enmascaro para pasar a word
	pand	xmm5, xmm7
	movdqu	xmm6, %1			;cargo y me quedo con el pixel una a derecha
	pslldq	xmm6, 1
	pand	xmm6, xmm7
%if %3 = 0
	psllw	xmm6, 1			;multiplico por dos
%endif
	paddusw	xmm5, xmm6		;sumo el segundo pixel
	movdqu	xmm6, %1
	pslldq	xmm6, 2			;cargo y me quedo con el pixel dos a derecha
	pand	xmm6, xmm7
	paddusw	xmm5, xmm6		; ya tengo acumulado en xmm5 la parte positiva de los pixeles, 2, 4, 6, 8

	movdqu	xmm6, %2
	pand	xmm6, xmm7
	psubusw	xmm5, xmm6
	movdqu	xmm6, %2
	pslldq	xmm6, 1
	pand	xmm6, xmm7
%if %3 = 0
	psllw	xmm6, 1			;multiplico por dos
%endif	
	psubusw	xmm5, xmm6
	movdqu	xmm6, %2
	pslldq	xmm6, 2			;cargo y me quedo con el pixel dos a derecha
	pand	xmm6, xmm7
	psubusw	xmm5, xmm6		; ya el operador aplicado en xmm5 a los pixeles, 2, 4, 6, 8
	pxor	xmm6, xmm6
	packuswb	xmm5, xmm6
	punpcklbw	xmm5, xmm6	;saturo los pixeles
	movdqu	xmm6, [eax]
	por	xmm5, xmm6
	movdqu	[eax], xmm5		;copio los pixeles

	;**********************
	; PROCESO PIXEL 1,3,5
	;**********************
	movdqu	xmm5, %1			;cargo el primer registro y enmascaro para pasar a word
	psrldq	xmm5, 1
	pand	xmm5, xmm7
	movdqu	xmm6, %1			;cargo y me quedo con el pixel una a derecha
	pand	xmm6, xmm7
%if %3 = 0
	psllw	xmm6, 1			;multiplico por dos
%endif
	paddusw	xmm5, xmm6		;sumo el segundo pixel
	movdqu	xmm6, %1
	pslldq	xmm6, 1			;cargo y me quedo con el pixel dos a derecha
	pand	xmm6, xmm7
	paddusw	xmm5, xmm6		; ya tengo acumulado en xmm5 la parte positiva de los pixeles, 1, 3, 5

	movdqu	xmm6, %2
	psrldq	xmm6, 1
	pand	xmm6, xmm7
	psubusw	xmm5, xmm6
	movdqu	xmm6, %2
	pand	xmm6, xmm7
%if %3 = 0
	psllw	xmm6, 1			;multiplico por dos
%endif	
	psubusw	xmm5, xmm6
	movdqu	xmm6, %2
	pslldq	xmm6, 1			;cargo y me quedo con el pixel dos a derecha
	pand	xmm6, xmm7
	psubusw	xmm5, xmm6		; ya el operador aplicado en xmm5 a los pixeles, 2, 4, 6, 8

	pxor	xmm6, xmm6
	packuswb	xmm5, xmm6
	punpcklbw	xmm5, xmm6	;saturo los pixeles
	pslldq	xmm5, 1
	movdqu	xmm6, [eax]
	por	xmm5, xmm6
	movdqu	[eax], xmm5		;copio los pixeles

	add		eax, edx	;salto la linea siguiente
%endmacro


;========================
; MACRO sobelPrewitt
;========================
; Cuerpo de codigo que aplica los operadores de Sobel y Prewitt
; Entrada:
;	esSobel		marca si es sobel o no
;========================
%macro sobelPrewitt 1
	doEnter RESERVED_BYTES


	xor	edx, edx
	mov	eax, SRC
	mov	eax, [eax + WIDTH_STEP]
	mov	ebx, STEP_X
	div	ebx
	mov	REMAINDER, edx		;consigo el resto del ancho respecto de 6

	mov	edi, SRC		;edi <-- *SRC
	mov	edi, [edi + IMAGE_DATA]
	
	mov	edx, SRC
	mov	edx, [edx + WIDTH_STEP]

	mov	esi, DST		;esi <-- *DST
	mov	esi, [esi + IMAGE_DATA]
	inc 	esi
	add	esi, edx		;esi apunta a la segunda fila segunda columna

	mov	ebx, HEIGHT		;va a ser mi variable de altura
	
	mov	eax, XORDER

	cmp	eax, 0
	jne	%%procesaX	
	jmp	%%preProcesaY
%%procesaX:

%%cicloSX_y:
	xor	ecx, ecx
%%cicloSX_x:
 	pxor	xmm6, xmm6		;limpio A
	pxor	xmm7, xmm7		;limpio B

	xor	eax, eax		;offset de linea

	movdqu	xmm0, [edi]		;cargo las seis lineas correspondientes
	add	eax, edx		;salto una linea
	movdqu	xmm1, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm2, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm3, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm4, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm5, [edi + eax]		
		
	mov	eax, esi

	;******************************
	; PROCESO LAS LINEAS QUE CARGUE
	;******************************
	sobelPrewittX xmm0, xmm1, xmm2, %1
	sobelPrewittX xmm1, xmm2, xmm3, %1
	sobelPrewittX xmm2, xmm3, xmm4, %1
	sobelPrewittX xmm3, xmm4, xmm5, %1


	add 	esi, STEP_X		
	add	edi, STEP_X
	add	ecx, STEP_X

	cmp	ecx, edx
	jl	%%cicloSX_x

	cmp	ebx, 4
	jg	%%procesaSX
	jmp	%%noProcesaSX
%%procesaSX:

	;******************************
	; PROCESO EL RESTO DE
	; LAS LINEAS QUE CARGUE
	;******************************
	sub	esi, FINAL_STEP
	sub	edi, FINAL_STEP

 	pxor	xmm6, xmm6		;limpio A
	pxor	xmm7, xmm7		;limpio B

	xor	eax, eax		;offset de linea

	movdqu	xmm0, [edi]		;cargo las seis lineas correspondientes
	add	eax, edx		;salto una linea
	movdqu	xmm1, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm2, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm3, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm4, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm5, [edi + eax]

	mov	eax, esi

	sobelPrewittX xmm0, xmm1, xmm2, %1, 1
	sobelPrewittX xmm1, xmm2, xmm3, %1, 1
	sobelPrewittX xmm2, xmm3, xmm4, %1, 1
	sobelPrewittX xmm3, xmm4, xmm5, %1, 1

	add	esi, FINAL_STEP
	add	edi, FINAL_STEP

%%noProcesaSX:
 	mov	eax, REMAINDER
 
 	sub	esi, eax
 	sub	edi, eax

	mov	eax, edx
	add	eax, edx
	add	eax, edx
	add	esi, eax
	add	edi, eax	;esi debiera apuntar a la segunda columna de la fila
				;cuatro lugares mas abajo de la primera fila anterior
	sub	ebx, 4		;aca debo ver si me pase del area de la imagen y sino saltar
	cmp	ebx, 0
	jge	%%cicloSX_y		

%%preProcesaY:

	mov	eax, YORDER
	cmp	eax, 0
	jne	%%procesaY	
	jmp	%%fin


%%procesaY:
	xor	edx, edx
	mov	eax, SRC
	mov	eax, [eax + WIDTH_STEP]
	mov	ebx, STEP_Y
	div	ebx
	mov	REMAINDER, edx		;consigo el resto del ancho respecto de 6

	mov	edi, SRC		;edi <-- *SRC
	mov	edi, [edi + IMAGE_DATA]

	mov	edx, SRC
	mov	edx, [edx + WIDTH_STEP]
	
	mov	esi, DST		;esi <-- *DST
	mov	esi, [esi + IMAGE_DATA]
	inc 	esi
	add	esi, edx		;esi apunta a la segunda fila segunda columna

	mov	ebx, HEIGHT		;va a ser mi variable de altura

	pxor	xmm7, xmm7		;limpio B
	pcmpeqb	xmm7, xmm7		;armo la mascara, paso todo a uno
	psllw	xmm7, 8
	psrlw	xmm7, 8			;meto ceros intercalados de a byte
	psrldq	xmm7, 2		;paso el Ãºltimo word a cero
	pslldq	xmm7, 2

%%cicloSY_y:
	xor	ecx, ecx
%%cicloSY_x:
	xor	eax, eax		;offset de linea

	movdqu	xmm0, [edi]		;cargo las seis lineas correspondientes
	add	eax, edx		;salto una linea
	movdqu	xmm1, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm2, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm3, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm4, [edi + eax]		
		
	mov	eax, esi

	;******************************
	; PROCESO LAS LINEAS QUE CARGUE
	;******************************
	sobelPrewittY xmm2, xmm0, %1
	sobelPrewittY xmm3, xmm1, %1
	sobelPrewittY xmm4, xmm2, %1

	add 	esi, STEP_Y		
	add	edi, STEP_Y
	add	ecx, STEP_Y

	cmp	ecx, edx
	jl	%%cicloSY_x

	cmp	ebx, 3
	jg	%%procesaSY
	jmp	%%noProcesaSY
%%procesaSY:


	;******************************
	; PROCESO EL RESTO DE
	; LAS LINEAS QUE CARGUE
	;******************************

	sub	esi, FINAL_STEP
	sub	edi, FINAL_STEP

	xor	eax, eax		;offset de linea

	movdqu	xmm0, [edi]		;cargo las seis lineas correspondientes
	add	eax, edx		;salto una linea
	movdqu	xmm1, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm2, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm3, [edi + eax]		
	add	eax, edx		;salto una linea
	movdqu	xmm4, [edi + eax]		
		
	mov	eax, esi

	sobelPrewittY xmm2, xmm0, %1
	sobelPrewittY xmm3, xmm1, %1
	sobelPrewittY xmm4, xmm2, %1

	add	esi, FINAL_STEP
	add	edi, FINAL_STEP

%%noProcesaSY:
	mov	eax, REMAINDER

	sub	esi, eax
	sub	edi, eax

	mov	eax, edx
	add	eax, edx
	add	esi, eax
	add	edi, eax	;esi debiera apuntar a la segunda columna de la fila
				;cuatro lugares mas abajo de la primera fila anterior
	sub	ebx, 3		;aca debo ver si me pase del area de la imagen y sino saltar
	cmp	ebx, 0
	jge	%%cicloSY_y		

%%fin:
	doLeave RESERVED_BYTES, 1
%endmacro